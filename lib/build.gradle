plugins {
    id 'com.android.library'
    id 'maven-publish'
    id "com.jfrog.bintray" version "1.8.5"
}

def mVersion = "1.1.0.RC"

android {
    compileSdkVersion 30

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 30
        versionCode 1
        versionName mVersion

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
}

def pomConfig = {
    licenses {
        license {
            name "MIT License"
            url "https://github.com/MrNinso/BaseUtils/blob/master/LICENSE"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "developer3"
            name "DeveloperÂ³"
            email "jheison540as@gmail.com"
        }
    }

    scm {
        url "https://github.com/MrNinso/BaseUtils"
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier.set("sourcesJar")
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

javadoc.failOnError(false)
task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    archiveClassifier.set("javadoc")
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            groupId 'com.developer3'
            artifactId 'BaseUtils'
            version mVersion

            artifact sourcesJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }

            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'BaseUtils is a toolbox to help you interact with the android system and codeless.')
                root.appendNode('name', 'BaseUtils')
                root.appendNode('url', 'https://github.com/MrNinso/BaseUtils')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = System.getenv('USERNAME')
    key = System.getenv('TOKEN')
    publications = ['mavenPublication']

    pkg {
        repo = 'com.developer3.libs'
        name = 'BaseUtils'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/MrNinso/BaseUtils.git'
        version {
            name = mVersion
            desc = mVersion
            released = new Date()
            gpg{
                sign = true
            }
        }
    }

    publish = true
}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'com.google.android.material:material:1.1.0'

    testImplementation 'junit:junit:4.13'
    testImplementation 'org.robolectric:robolectric:4.0' //TODO
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}
